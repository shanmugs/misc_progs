buildscript {
	ext {
		// this sets the Spring Boot version used by the spring dependency
		// management plugin
		springBootVersion = '2.0.5.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
// spring boot plugin, adds the bootRun task (among other things)
apply plugin: 'org.springframework.boot'
// spring dependency management (handles loading correct versions
// for spring dependencies)
apply plugin: 'io.spring.dependency-management'

group = 'com.okta.springbootwithauth'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	// okta's helpful spring boot starter
	compile('com.okta.spring:okta-spring-boot-starter:0.6.0')
	// spring boot security
	compile('org.springframework.boot:spring-boot-starter-security')
	// spring boot web server
	compile('org.springframework.boot:spring-boot-starter-web')
	// thymeleaf templating support for spring boot
	compile("org.springframework.boot:spring-boot-starter-thymeleaf")
	// integrates spring boot auth and thymeleaf templating
	compile ("org.thymeleaf.extras:thymeleaf-extras-springsecurity4")
	// required for the @@EnableOAuth2Sso annotation
	compile ('org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.0.5.RELEASE')
	// for spring boot testing
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
}

/*
	This is required to resolve a logging dependency conflict between the
	okta-spring-boot-starter and the various spring dependencies.
 */
configurations.all {
	exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	exclude group: 'org.springframework.boot', module: 'logback-classic'
}